import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.cyclonedx.bom' version '1.10.0'
    id 'org.openapi.generator' version '7.9.0'
    id 'org.owasp.dependencycheck' version '11.1.0'
    id 'jacoco'
}

group = parent.group
version = parent.version

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'

    // for generated code
    implementation 'org.springframework:spring-webflux'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

var generatePetstore =tasks.register('generatePetstore', GenerateTask) {
    generatorName.set('java')
    inputSpec.set("$layout.projectDirectory/src/main/openapi/petstore.json")
    outputDir.set(layout.buildDirectory.get().dir("generated/sources/petstore").getAsFile().getAbsolutePath())
    apiPackage.set("org.angnysa.petstore.frontend.rest.generated.api")
    modelPackage.set("org.angnysa.petstore.frontend.rest.generated.model")
    configOptions.set([
            // https://openapi-generator.tech/docs/generators/java
            dateLibrary                : "java8",
            library                    : "webclient",
            useBeanValidation          : "true",
            useJakartaEe               : "true",
            webclientBlockingOperations: "true"
    ])
}.get()

compileJava.dependsOn generatePetstore

sourceSets.main.java.srcDirs += generatePetstore.outputDir.get() + "/src/main/java"

// dependency check

//check.dependsOn dependencyCheckAnalyze

